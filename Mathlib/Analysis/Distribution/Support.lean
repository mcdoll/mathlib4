/-
Copyright (c) 2026 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.TemperedDistribution
import Mathlib.Analysis.Calculus.BumpFunction.FiniteDimension
import Mathlib.Geometry.Manifold.PartitionOfUnity

/-! # Support of distributions

We define the support of a distribution, `dsupport u`, as the intersection of all closed sets on
which `u` vanishes.
For this we also define a predicate `IsVanishingOn` that asserts that a map `f : F ‚Üí V` vanishes on
`s : Set Œ±` if for all `u : F` with `tsupport u ‚äÜ s` it follows that `f u = 0`.

These definitions work independently of a specific class of distributions (classical, tempered, or
compactly supported) and all basic properties are proved an abstract setting using `FunLike`.

## Main definitions
* `IsVanishingOn`: A distribution vanishes on a set if it acts trivially on all test functions
  supported in that subset.
* `dsupport`: The support of a distribution is the intersection of all closed sets on which that
  distribution vanishes.

## Main statements
* `TemperedDistribution.dsupport_delta`: The support of the delta distribution is a single point.

-/

@[expose] public noncomputable section

variable {Œπ Œ± Œ≤ ùïú E F F‚ÇÅ F‚ÇÇ R V : Type*}

section abstract

variable [FunLike F Œ± Œ≤] [TopologicalSpace Œ±] [Zero Œ≤]

variable {f g : F ‚Üí V} {s s‚ÇÅ s‚ÇÇ : Set Œ±}

section IsVanishingOn

/-! ## Vanishing of distributions -/

section Zero

variable [Zero V]

/-- A distribution `f` vanishes on `s` if for all `u` with `tsupport u ‚äÜ s` it follows that
`f u = 0`. -/
def IsVanishingOn (f : F ‚Üí V) (s : Set Œ±) : Prop :=
    ‚àÄ (u : F), tsupport u ‚äÜ s ‚Üí f u = 0

variable (F V s) in
@[simp, grind .]
theorem isVanishingOn_zero : IsVanishingOn (0 : F ‚Üí V) s := by
  simp [IsVanishingOn]

@[simp]
theorem isVanishingOn_univ_iff : IsVanishingOn f Set.univ ‚Üî f = 0 := by
  refine ‚ü®fun hf ‚Ü¶ ?_, fun hf ‚Ü¶ by simp [hf]‚ü©
  ext u
  simpa [IsVanishingOn] using hf u

theorem IsVanishingOn.mono (hs : s‚ÇÇ ‚äÜ s‚ÇÅ) (hf : IsVanishingOn f s‚ÇÅ) : IsVanishingOn f s‚ÇÇ :=
  fun u hu ‚Ü¶ hf u (hu.trans hs)

end Zero

section Monoid

@[grind .]
theorem IsVanishingOn.add [AddMonoid V] (hf : IsVanishingOn f s‚ÇÅ) (hg : IsVanishingOn g s‚ÇÇ) :
    IsVanishingOn (f + g) (s‚ÇÅ ‚à© s‚ÇÇ) := by
  intro u hu
  simp [hf u (hu.trans Set.inter_subset_left), hg u (hu.trans Set.inter_subset_right)]

variable [SubtractionMonoid V]

@[grind .]
theorem IsVanishingOn.neg (hf : IsVanishingOn f s) :
    IsVanishingOn (-f) s := by
  intro u hu
  simpa using hf u hu

@[grind .]
theorem IsVanishingOn.sub (hf : IsVanishingOn f s‚ÇÅ)
    (hg : IsVanishingOn g s‚ÇÇ) :
    IsVanishingOn (f - g) (s‚ÇÅ ‚à© s‚ÇÇ) := by
  intro u hu
  simp [hf u (hu.trans Set.inter_subset_left), hg u (hu.trans Set.inter_subset_right)]

end Monoid

section Module

variable [Semiring R] [AddCommMonoid V] [Module R V]

@[grind .]
theorem IsVanishingOn.smul (hf : IsVanishingOn f s) (r : R) :
    IsVanishingOn (r ‚Ä¢ f) s := by
  intro u hu
  simp [hf u hu]

end Module

end IsVanishingOn

section dsupport

/-! ## Support -/

section Zero

variable [Zero V]

/-- The distributional support is the smallest closed subset of `E` on which a distribution does not
vanish. -/
def dsupport (f : F ‚Üí V) : Set Œ± := ‚ãÇ‚ÇÄ { s | IsVanishingOn f s·∂ú ‚àß IsClosed s}

theorem mem_dsupport_iff (x : Œ±) :
    x ‚àà dsupport f ‚Üî ‚àÄ (s : Set Œ±), IsVanishingOn f s·∂ú ‚Üí IsClosed s ‚Üí x ‚àà s := by
  simp [dsupport]

theorem mem_dsupport_of_forall_exists_ne (x : Œ±) (h : ‚àÄ (s : Set Œ±) (_ : x ‚àà s) (_ : IsOpen s),
    ‚àÉ u : F, tsupport u ‚äÜ s ‚àß f u ‚â† 0) : x ‚àà dsupport f := by
  rw [mem_dsupport_iff]
  intro s hs hs'
  by_cases! h' : x ‚àà s
  ¬∑ exact h'
  exfalso
  obtain ‚ü®u, h‚ÇÅ, h‚ÇÇ‚ü© := h s·∂ú h' IsClosed.isOpen_compl
  exact h‚ÇÇ (hs u h‚ÇÅ)

@[simp high]
theorem mem_dsupport_compl_iff (x : Œ±) :
    x ‚àà (dsupport f)·∂ú ‚Üî ‚àÉ (s : Set Œ±), IsVanishingOn f s ‚àß IsOpen s ‚àß x ‚àà s := by
  simp only [dsupport, Set.mem_compl_iff, Set.mem_sInter, Set.mem_setOf_eq, and_imp, not_forall]
  constructor
  ¬∑ intro ‚ü®s, hs‚ÇÅ, hs‚ÇÇ, h‚ü©
    use s·∂ú, hs‚ÇÅ, IsClosed.isOpen_compl
    exact h
  ¬∑ intro ‚ü®s, hs‚ÇÅ, hs‚ÇÇ, h‚ü©
    use s·∂ú
    simp only [Set.mem_compl_iff, not_not, isClosed_compl_iff, exists_prop, compl_compl]
    exact ‚ü®hs‚ÇÅ, hs‚ÇÇ, h‚ü©

/-- The complement of the support is given by all open sets on which `f` vanishes. -/
theorem dsupport_compl_eq : (dsupport f)·∂ú = ‚ãÉ‚ÇÄ { a | IsVanishingOn f a ‚àß IsOpen a } := by
  simp [dsupport, Set.compl_sInter, Set.compl_image_set_of]

@[grind .]
theorem dsupport_subset_dsupport
    (h : ‚àÄ (s : Set Œ±) (_ : IsOpen s), IsVanishingOn g s ‚Üí IsVanishingOn f s) :
    dsupport f ‚äÜ dsupport g := by
  intro x hx
  rw [mem_dsupport_iff] at hx ‚ä¢
  intro s hs hs'
  apply hx s (h s·∂ú IsClosed.isOpen_compl hs) hs'

@[grind .]
theorem isClosed_dsupport : IsClosed (dsupport f) := by
  grind [dsupport, isClosed_sInter]

@[simp]
theorem support_zero_eq_emptyset : dsupport (0 : F ‚Üí V) = ‚àÖ := by
  simp only [dsupport, isVanishingOn_zero, true_and, Set.sInter_eq_empty_iff, Set.mem_setOf_eq]
  intro x
  use ‚àÖ
  simp

end Zero

section Monoid

theorem support_add_subset [AddMonoid V] : dsupport (f + g) ‚äÜ dsupport f ‚à™ dsupport g := by
  rw [‚Üê Set.compl_subset_compl, Set.compl_union]
  intro x hx
  rw [mem_dsupport_compl_iff]
  simp only [Set.mem_inter_iff, mem_dsupport_compl_iff] at hx
  obtain ‚ü®‚ü®s‚ÇÅ, hs‚ÇÅ, hs‚ÇÅ', hs‚ÇÅ''‚ü©, s‚ÇÇ, hs‚ÇÇ, hs‚ÇÇ', hs‚ÇÇ''‚ü© := hx
  use s‚ÇÅ ‚à© s‚ÇÇ
  exact ‚ü®hs‚ÇÅ.add hs‚ÇÇ, hs‚ÇÅ'.inter hs‚ÇÇ', Set.mem_inter hs‚ÇÅ'' hs‚ÇÇ''‚ü©

variable [SubtractionMonoid V]

@[simp]
theorem support_neg_eq : dsupport (-f) = dsupport f := by
  apply subset_antisymm
  all_goals grind [neg_neg]

theorem support_sub_subset : dsupport (f - g) ‚äÜ dsupport f ‚à™ dsupport g := by
  grw [sub_eq_add_neg, support_add_subset, support_neg_eq]

end Monoid

section Module

variable [Semiring R] [AddCommMonoid V] [Module R V]

theorem support_smul_subset (r : R) : dsupport (r ‚Ä¢ f) ‚äÜ dsupport f := by grind

end Module

end dsupport

end abstract

section normed

variable [FunLike F Œ± Œ≤] [SeminormedAddGroup Œ±] [Zero Œ≤] [Zero V]

variable {f : F ‚Üí V}

/-- The complement of the support is given by all *bounded* open sets on which `f` vanishes. -/
theorem support_compl_eq_sUnion_isBounded :
    (dsupport f)·∂ú = ‚ãÉ‚ÇÄ { a | IsVanishingOn f a ‚àß IsOpen a ‚àß Bornology.IsBounded a } := by
  rw [dsupport_compl_eq]
  apply subset_antisymm
  ¬∑ simp only [Set.sUnion_subset_iff, Set.mem_setOf_eq, and_imp]
    intro s hs‚ÇÅ hs‚ÇÇ
    have : s = ‚ãÉ (Œµ : ‚Ñù) (_ : 0 < Œµ), s ‚à© Metric.ball 0 Œµ := by
      have : ‚ãÉ (Œµ : ‚Ñù) (_ : 0 < Œµ), Metric.ball (0 : Œ±) Œµ = Set.univ := by
        rw [Set.iUnion‚ÇÇ_eq_univ_iff]
        intro x
        use ‚Äñx‚Äñ + 1, by positivity
        simp
      simp [‚Üê Set.inter_iUnion‚ÇÇ, this]
    rw [this]
    simp only [Set.iUnion_subset_iff]
    intro Œµ hŒµ
    apply Set.subset_sUnion_of_mem
    refine ‚ü®hs‚ÇÅ.mono Set.inter_subset_left, hs‚ÇÇ.inter Metric.isOpen_ball, ?_‚ü©
    exact Bornology.IsBounded.subset Metric.isBounded_ball Set.inter_subset_right
  simp only [Set.sUnion_subset_iff, Set.mem_setOf_eq, and_imp]
  intro s hs‚ÇÅ hs‚ÇÇ hs‚ÇÉ
  exact Set.subset_sUnion_of_mem ‚ü®hs‚ÇÅ, hs‚ÇÇ‚ü©

end normed

namespace TemperedDistribution

/-! ## Tempered distributions -/

open SchwartzMap ContinuousLinearMap MeasureTheory MeasureTheory.Measure

open scoped Nat NNReal ContDiff

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace ‚Ñù E] [NormedSpace ‚ÑÇ F]

variable {f : ùì¢'(E, F)} {g : ùì¢'(E, F)} {s s‚ÇÅ s‚ÇÇ : Set E}

section IsVanishingOn

open scoped Topology

@[grind .]
theorem _root_.IsVanishingOn.smulLeftCLM (hf : IsVanishingOn f s) {g : E ‚Üí ‚ÑÇ}
    (hg : g.HasTemperateGrowth) :
    IsVanishingOn (smulLeftCLM F g f) s := by
  intro u hu
  apply hf ((SchwartzMap.smulLeftCLM ‚ÑÇ g) u)
  rw [SchwartzMap.smulLeftCLM_apply hg]
  exact (tsupport_smul_subset_right g u).trans hu

open LineDeriv

@[grind .]
theorem _root_.IsVanishingOn.lineDerivOp (hf : IsVanishingOn f s) (m : E) :
    IsVanishingOn (‚àÇ_{m} f : ùì¢'(E, F)) s := by
  intro u hu
  simp only [lineDerivOp_apply_apply, map_neg, neg_eq_zero]
  exact hf (‚àÇ_{m} u) <| (tsupport_fderiv_apply_subset ‚Ñù m).trans hu

@[grind .]
theorem _root_.IsVanishingOn.iteratedLineDerivOp {n : ‚Ñï} (hf : IsVanishingOn f s) (m : Fin n ‚Üí E) :
    IsVanishingOn (‚àÇ^{m} f : ùì¢'(E, F)) s := by
  induction n with
  | zero =>
    exact hf
  | succ n IH =>
    exact (IH <| Fin.tail m).lineDerivOp (m 0)

@[grind .]
theorem isVanishingOn_delta (x : E) : IsVanishingOn (delta x) {x}·∂ú := by
  intro u hu
  rw [Set.subset_compl_singleton_iff] at hu
  apply image_eq_zero_of_notMem_tsupport hu

end IsVanishingOn

section Support

theorem dsupport_smulLeftCLM_subset {g : E ‚Üí ‚ÑÇ} (hg : g.HasTemperateGrowth) :
    dsupport (smulLeftCLM F g f) ‚äÜ dsupport f := by grind

open LineDeriv

theorem dsupport_lineDerivOp_subset (m : E) : dsupport (‚àÇ_{m} f : ùì¢'(E, F)) ‚äÜ dsupport f := by grind

theorem dsupport_iteratedLineDerivOp_subset {n : ‚Ñï} (m : Fin n ‚Üí E) :
    dsupport (‚àÇ^{m} f : ùì¢'(E, F)) ‚äÜ dsupport f := by grind

theorem dsupport_delta [FiniteDimensional ‚Ñù E] (x : E) : dsupport (delta x) = {x} := by
  apply subset_antisymm
  ¬∑ intro x' hx'
    rw [mem_dsupport_iff] at hx'
    exact hx' {x} (isVanishingOn_delta x) (T1Space.t1 x)
  rintro x rfl
  apply mem_dsupport_of_forall_exists_ne
  intro s hx hs
  obtain ‚ü®u, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, -, h‚ÇÑ‚ü© :=
    exists_contDiff_tsupport_subset (n := ‚ä§) ((IsOpen.mem_nhds_iff hs).mpr hx)
  have h‚ÇÅ' : tsupport (Complex.ofRealCLM ‚àò u) ‚äÜ s := (tsupport_comp_subset rfl _).trans h‚ÇÅ
  have h‚ÇÇ' : HasCompactSupport (Complex.ofRealCLM ‚àò u) := h‚ÇÇ.comp_left rfl
  use h‚ÇÇ'.toSchwartzMap (Complex.ofRealCLM.contDiff.comp h‚ÇÉ)
  exact ‚ü®h‚ÇÅ', by simp [h‚ÇÑ]‚ü©

end Support

end TemperedDistribution
